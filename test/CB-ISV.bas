1 REM GETTING STARTED WITH COLOR BASIC
4 REM INVENTORY SHOPPING LIST, P295
5 CLEAR 2000: DIM S$(100)
10 REM INVENTORY/SHOPPING LIST
20 CLS
30 PRINT @ 71, "DO YOU WANT TO--"
40 PRINT @ 134, "(1) INPUT ITEMS"
50 PRINT @ 166, "(2) REPLACE ITEMS"
60 PRINT @ 198, "(3) ADD TO THE LIST"
70 PRINT @ 230, "(4) DELETE ITEMS"
80 PRINT @ 262, "(5) PRINT ALL ITEMS"
90 PRINT @ 294, "(6) SAVE ITEMS TO TAPE"
100 PRINT @ 326, "(7) LOAD ITEMS FROM TAPE"
110 PRINT @ 395, "(1-7)";
120 INPUT M
130 IF M<0 OR M>7 THEN 10
140 ON M GOSUB 1000, 2000, 1020, 3000, 4000, 5000, 6000
150 GOTO 10
900 REM
1000 REM INPUT/ADD ITEMS
1010 Y = 1
1020 CLS:PRINT @ 8, "INPUT/ADD ITEMS"
1030 PRINT @ 34, "PRESS <ENTER> WHEN FINISHED"
1040 PRINT:PRINT "ITEM" Y;
1045 INPUT S$(Y)
1050 IF S$(Y) = "" THEN RETURN
1060 Y = Y + 1
1070 GOTO 1040
1900 REM
2000 REM REPLACE ITEMS
2005 N = 0
2010 CLS:PRINT @ 9, "REPLACE ITEMS"
2020 PRINT @ 34, "PRESS <ENTER> WHEN FINISHED"
2030 PRINT:INPUT "ITEM NO. TO REPLACE";N
2040 IF N = 0 THEN RETURN
2050 INPUT "REPLACEMENT ITEM";S$(N)
2060 GOTO 2000
2900 REM
3000 REM DELETE ITEMS
3005 N = 0
3010 CLS: PRINT @ 9, "DELETE ITEMS"
3020 PRINT @ 34, "PRESS <ENTER> WHEN FINSIHED"
3030 PRINT: INPUT "ITEM TO DELETE";N
3035 IF N > Y-1 THEN 3030
3040 IF N = 0 THEN RETURN
3050 FOR X = N TO Y-2
3060 S$(X) = S$(X+1)
3070 NEXT X
3080 S$(X) = ""
3090 Y = Y-1
3100 GOTO 3000
3900 REM
4000 REM PRINT ITEMS
4010 FOR X = 1 TO Y-1 STEP 15
4020 FOR Z = X TO X+14
4030 PRINT Z; S$(Z)
4040 NEXT Z
4150 INPUT "PRESS <ENTER> TO CONTINUE";C$
4060 NEXT X
4070 RETURN
4900 REM
5000 REM SAVE ITEMS ON TAPE
5010 CLS: PRINT @ 135, "SAVE ITEMS TO TAPE"
5020 PRINT @ 234, "POSITION TAPE"
5030 PRINT @ 294, "PRESS PLAY AND RECORD"
5040 PRINT @ 388, "PRESS <ENTER> WHEN READY"
5050 INPUT R$
5060 OPEN "O", #-1, "LIST"
5070 FOR X = 1 TO Y-1
5080 PRINT #-1, S$(X)
5090 NEXT X
5100 CLOSE #-1: RETURN
5900 REM
6000 REM LOAD ITEMS FROM TAPE
6010 CLS: PRINT @ 136, "LOAD ITEMS FROM TAPE"
6020 PRINT @ 235, "REWIDE TAPE"
6030 PRINT @ 300, "PRESS PLAY"
6040 PRINT @ 388, "PRESS <ENTER> WHEN READY"
6050 INPUT R$
6060 OPEN "I", #-1, "LIST"
6070 Y = 1
6080 IF EOF(-1) THEN 6120
6090 INPUT #-1, S$(Y)
6095 PRINT S$(Y)
6100 Y = Y + 1
6110 GOTO 6080
6120 CLOSE #-1: RETURN
